//*********************************************************************************
//* CALIBRE ANTENNA DRC COMMAND FILE  - CN65S_6M.ANT.20a 10/16/2009
//* FOR TSMC 65NM/55NM CMOS LOGIC 1P6M+APRDL PROCESS
//* DESIGN RULE DOCUMENT: 	T-N65-CL-DR-001    VER 2.0
//* DRC COMMAND FILE DOCUMENT: 	T-N65-CL-DR-001-C1 VER 2.0a
//*********************************************************************************

//*************************************************************************************
// TSMC has developed this deck using Mentor Graphics proprietary SVRF and TVF formats.
// The deck is to be used only in Calibre tools.
//*************************************************************************************


//#DEFINE 28K_AP


//
// ENVIRONMENT SETUP
//==================
PRECISION    1000
RESOLUTION      5

LAYOUT SYSTEM GDSII
LAYOUT PATH "GDSFILENAME"
LAYOUT PRIMARY "TOPCELLNAME"

DRC RESULTS DATABASE "DRC_RES.db"
DRC SUMMARY REPORT "DRC.rep"
DRC KEEP EMPTY NO
DRC CHECK TEXT ALL
DRC MAXIMUM RESULTS ALL

FLAG OFFGRID YES
FLAG ACUTE YES
FLAG SKEW YES
FLAG NONSIMPLE YES
DRC INCREMENTAL CONNECT YES

VARIABLE LargeNumber 1000000000

// DRAWN LAYER DEFINITIONS
//========================
LAYER  OD_18     16  // define 1.8V thick gate oxides
LAYER  OD_25     18  // define 2.5V thick gate oxides
LAYER  OD_33     15  // define 3.3V thick gate oxides
LAYER  CO        30
LAYER  VIA1      51     // VIA1 Define connect for M2 to M1
LAYER  VIA2      52     // VIA2 Define connect for M3 to M2
LAYER  VIA3      53     // VIA3 Define connect for M4 to M3
LAYER  VIA4      54     // VIA4 Define connect for M5 to M4
LAYER  VIA5      55     // VIA5 Define connect for M6 to M5

LAYER  M1     331  // Metal1 layer
LAYER MAP 31 DATATYPE 0  331  // Mapping (31;0)  to 331 
LAYER MAP 31 DATATYPE 7  331  // Mapping (31;7)  to 331
LAYER MAP 31 DATATYPE 20 331  // Mapping (31;20) to 331 
LAYER MAP 31 DATATYPE 40 331  // Mapping (31;40) to 331
LAYER MAP 31 DATATYPE 60 331  // Mapping (31;60) to 331
LAYER MAP 31 DATATYPE 80 331  // Mapping (31;80) to 331
LAYER  M2     332  // Metal2 layer
LAYER MAP 32 DATATYPE 0  332  // Mapping (32;0)  to 332 
LAYER MAP 32 DATATYPE 7  332  // Mapping (32;7)  to 332
LAYER MAP 32 DATATYPE 20 332  // Mapping (32;20) to 332 
LAYER MAP 32 DATATYPE 40 332  // Mapping (32;40) to 332
LAYER MAP 32 DATATYPE 60 332  // Mapping (32;60) to 332
LAYER MAP 32 DATATYPE 80 332  // Mapping (32;80) to 332
LAYER  M3     333  // Metal3 layer
LAYER MAP 33 DATATYPE 0  333  // Mapping (33;0)  to 333 
LAYER MAP 33 DATATYPE 7  333  // Mapping (33;7)  to 333
LAYER MAP 33 DATATYPE 20 333  // Mapping (33;20) to 333 
LAYER MAP 33 DATATYPE 40 333  // Mapping (33;40) to 333
LAYER MAP 33 DATATYPE 60 333  // Mapping (33;60) to 333
LAYER MAP 33 DATATYPE 80 333  // Mapping (33;80) to 333
LAYER  M4     334  // Metal4 layer
LAYER MAP 34 DATATYPE 0  334  // Mapping (34;0)  to 334 
LAYER MAP 34 DATATYPE 7  334  // Mapping (34;7)  to 334
LAYER MAP 34 DATATYPE 20 334  // Mapping (34;20) to 334 
LAYER MAP 34 DATATYPE 40 334  // Mapping (34;40) to 334
LAYER MAP 34 DATATYPE 60 334  // Mapping (34;60) to 334
LAYER MAP 34 DATATYPE 80 334  // Mapping (34;80) to 334
LAYER  M5     335  // Metal5 layer
LAYER MAP 35 DATATYPE 0  335  // Mapping (35;0)  to 335 
LAYER MAP 35 DATATYPE 7  335  // Mapping (35;7)  to 335
LAYER MAP 35 DATATYPE 20 335  // Mapping (35;20) to 335 
LAYER MAP 35 DATATYPE 40 335  // Mapping (35;40) to 335
LAYER MAP 35 DATATYPE 60 335  // Mapping (35;60) to 335
LAYER MAP 35 DATATYPE 80 335  // Mapping (35;80) to 335
LAYER  M6     336  // Metal6 layer
LAYER MAP 36 DATATYPE 0  336  // Mapping (36;0)  to 336 
LAYER MAP 36 DATATYPE 7  336  // Mapping (36;7)  to 336
LAYER MAP 36 DATATYPE 20 336  // Mapping (36;20) to 336 
LAYER MAP 36 DATATYPE 40 336  // Mapping (36;40) to 336
LAYER MAP 36 DATATYPE 60 336  // Mapping (36;60) to 336
LAYER MAP 36 DATATYPE 80 336  // Mapping (36;80) to 336

LAYER AP         74   // AP RDL 
LAYER Cu_PPI     7410 // Cu_PPI interconnection between Polymide1 and Polymide2 for WLCSP
LAYER MAP 74 DATATYPE 10 7410

LAYER RV         85  // RV Define connect for M6 to AP
LAYER CB         76  // CB Define connect for M6 to AP
LAYER CBD        169 // CBD Define connect for M6 to AP

LAYER MAP 6 DATATYPE 0 350  // Mapping (6;0) to 350 for 
LAYER MAP 7 DATATYPE 0 351  // Mapping (7;0) to 351 for PDIFF
LAYER MAP 8 DATATYPE 0 352  // Mapping (8;0) to 352 for NDIFF
LAYER DIFFi    350
LAYER PDIFFi   351  	    // PDIFF active areas
LAYER NDIFFi   352   	    // NDIFF active areas
LAYER POLY  365
LAYER MAP 17 DATATYPE 0 365  // Mapping (17;0) to 341 
OD = (DIFFi OR PDIFFi) OR NDIFFi

LAYOUT TOP LAYER M1 VIA1 
LAYOUT TOP LAYER M2 VIA2 
LAYOUT TOP LAYER M3 VIA3 
LAYOUT TOP LAYER M4 VIA4 
LAYOUT TOP LAYER M5 VIA5 
LAYOUT TOP LAYER M6 AP RV CB CBD 

GATE     = OD   AND POLY
SD       = OD   NOT POLY
CO_PO    = CO AND POLY
GATE_VIA = COPY GATE
OD2      = (OD_18 OR OD_25) OR OD_33
HV_GATE  = GATE AND OD2
HV18_GATE = GATE AND OD_18 
//POLY CHECKS
//===========

CONNECT GATE HV_GATE HV18_GATE GATE_VIA POLY

A.R.1.POLY { @ (Poly top area / gate area) <= 250
  NET AREA RATIO POLY GATE > 250
      [
         AREA(POLY)/AREA(GATE)
      ] RDB A.R.1.POLY.rep POLY GATE BY LAYER
}

// POLY thickness is 1000A
A.R.2.POLY { @ (Poly perim*thickness / gate area) <= 500
  NET AREA RATIO POLY GATE > 500
      [
         PERIMETER(POLY)*0.1/AREA(GATE)
      ] RDB A.R.2.POLY.rep POLY GATE BY LAYER
}

//CO CHECKS
//=========

CONNECT CO_PO POLY

A.R.3.CO { @ (contact area / gate area) <= 10 
  NET AREA RATIO POLY CO_PO GATE > 10  // error highlight on POLY
      [
	  AREA(CO_PO)/AREA(GATE)
      ] RDB A.R.3.CO.rep CO_PO GATE BY LAYER 
}

//M1 CHECKS
//=========

CONNECT M1 POLY SD BY CO
CONNECT VIA1 M1

M1_DIO = NET AREA SD >= 0

    
A.R.6_A.R.8.M1 { @ (M1 area / gate area) <= (1000 in OD_18,5000 not in OD_18) (without protection OD) 
		 @ (M1 area / gate area) <= OD area x 456 + 43000 (with protection OD) 
  NET AREA RATIO M1 M1_DIO HV18_GATE GATE > 0
       [
       !!AREA(M1) * !!AREA(GATE) *
	   (!AREA(M1_DIO) * (!!AREA(HV18_GATE)*(AREA(M1)/AREA(GATE)-1000) +
	                     !AREA(HV18_GATE)*(AREA(M1)/AREA(GATE)-5000)) +
	    !!AREA(M1_DIO) * ( AREA(M1)/AREA(GATE)-AREA(M1_DIO)*456-43000 ))
       ] RDB A.R.6_A.R.8.M1.rep M1 M1_DIO HV18_GATE GATE BY LAYER
}

ACC_M1 = NET AREA RATIO M1 GATE >=0 ACCUMULATE

//VIA1 CHECKS
//===========

A.R.4_A.R.7.VIA1 { @ (VIA1 area / gate area) <= 20 (without protection OD)
                   @ (VIA1 area / gate area) <= OD area x 210 + 900 (with protection OD)
  NET AREA RATIO M1 VIA1 M1_DIO GATE_VIA > 0
       [
       !!AREA(M1) * !!AREA(VIA1) * 
           (!!AREA(M1_DIO) * ( AREA(VIA1)/AREA(GATE_VIA)-AREA(M1_DIO)*210-900 ) +
	     !AREA(M1_DIO) * ( AREA(VIA1)/AREA(GATE_VIA)-20 ) )
       ] RDB A.R.4_A.R.7.VIA1.rep VIA1 M1_DIO GATE_VIA BY LAYER
}

ACC_VIA1 = NET AREA RATIO VIA1 GATE_VIA >=0 ACCUMULATE





//M2 CHECKS
//=========

CONNECT M2 M1 BY VIA1
CONNECT VIA2 M2

M2_DIO = NET AREA SD >= 0


A.R.6_A.R.8.M2 { @ (cumulative M1~M2 area / gate area) <= (1000 in OD_18 ,5000 not in OD_18) (without protection OD) 
		 @ (cumulative M1~M2 area / gate area) <= OD area x 456 + 43000 (with effective diode) 
  NET AREA RATIO M2 M1 M2_DIO HV18_GATE GATE > 0 ACCUMULATE ACC_M1
       [
       !!AREA(M2) * !!AREA(GATE) *
	   (!AREA(M2_DIO) * (!!AREA(HV18_GATE)*(AREA(M2)/AREA(GATE)-1000) +
	                     !AREA(HV18_GATE)*(AREA(M2)/AREA(GATE)-5000)) +
	    !!AREA(M2_DIO) * ( AREA(M2)/AREA(GATE)-AREA(M2_DIO)*456-43000 ))
       - (!AREA(M2)+!AREA(GATE)) * LargeNumber	    
       ] RDB A.R.6_A.R.8.M2.rep M2 M1 M2_DIO HV18_GATE GATE BY LAYER
}

ACC_M2 = NET AREA RATIO M2 GATE >= 0 ACCUMULATE ACC_M1

//VIA2 CHECKS
//===========

A.R.4.VIA2 { @ (VIA2 area / gate area) <= 20 (without protection OD)
  NET AREA RATIO VIA2 GATE_VIA M2_DIO > 20
      [
        !AREA(M2_DIO) * !!AREA(VIA2) * !!AREA(GATE_VIA) * ( AREA(VIA2)/AREA(GATE_VIA) )
      ] RDB A.R.4.VIA2.rep VIA2 GATE_VIA M2_DIO BY LAYER
}

A.R.7.VIA2 { @ (cumulative VIA1~VIA2 area / gate area) <= OD area x 210 + 900 (with protection OD)
  NET AREA RATIO VIA2 VIA1 M2_DIO GATE_VIA > 0 ACCUMULATE ACC_VIA1
       [
        !!AREA(VIA2) * !!AREA(M2_DIO) *
	   ( AREA(VIA2)/AREA(GATE_VIA)-AREA(M2_DIO)*210-900 ) 
       - (!AREA(VIA2)+!AREA(M2_DIO)) * LargeNumber	    
       ] RDB A.R.7.VIA2.rep VIA2 VIA1 M2_DIO GATE_VIA BY LAYER
}

A.R.9.VIA2 { @ (cumulative VIA1~VIA2 area / gate area) <= 50 (in OD2/without protection OD)  
  NET AREA RATIO VIA2 VIA1 HV_GATE M2_DIO GATE_VIA > 0 ACCUMULATE ACC_VIA1
      [
        !AREA(M2_DIO) * !!AREA(VIA2) * !!AREA(GATE_VIA) * !!AREA(HV_GATE) * ( AREA(VIA2)/AREA(GATE_VIA) - 50 )
        - (!!AREA(M2_DIO)+!AREA(VIA2)+!AREA(GATE_VIA)+!AREA(HV_GATE)) * LargeNumber
	
      ] RDB A.R.9.VIA2.rep VIA2 VIA1 GATE_VIA BY LAYER 
}


ACC_VIA2 = NET AREA RATIO VIA2 GATE_VIA >=0 ACCUMULATE ACC_VIA1



//M3 CHECKS
//=========

CONNECT M3 M2 BY VIA2
CONNECT VIA3 M3

M3_DIO = NET AREA SD >= 0


A.R.6_A.R.8.M3 { @ (cumulative M1~M3 area / gate area) <= (1000 in OD_18 ,5000 not in OD_18) (without protection OD) 
		 @ (cumulative M1~M3 area / gate area) <= OD area x 456 + 43000 (with effective diode) 
  NET AREA RATIO M3 M2 M1 M3_DIO HV18_GATE GATE > 0 ACCUMULATE ACC_M2
       [
       !!AREA(M3) * !!AREA(GATE) *
	   (!AREA(M3_DIO) * (!!AREA(HV18_GATE)*(AREA(M3)/AREA(GATE)-1000) +
	                     !AREA(HV18_GATE)*(AREA(M3)/AREA(GATE)-5000)) +
	    !!AREA(M3_DIO) * ( AREA(M3)/AREA(GATE)-AREA(M3_DIO)*456-43000 ))
       - (!AREA(M3)+!AREA(GATE)) * LargeNumber	    
       ] RDB A.R.6_A.R.8.M3.rep M3 M2 M1 M3_DIO HV18_GATE GATE BY LAYER
}

ACC_M3 = NET AREA RATIO M3 GATE >= 0 ACCUMULATE ACC_M2

//VIA3 CHECKS
//===========

A.R.4.VIA3 { @ (VIA3 area / gate area) <= 20 (without protection OD)
  NET AREA RATIO VIA3 GATE_VIA M3_DIO > 20
      [
        !AREA(M3_DIO) * !!AREA(VIA3) * !!AREA(GATE_VIA) * ( AREA(VIA3)/AREA(GATE_VIA) )
      ] RDB A.R.4.VIA3.rep VIA3 GATE_VIA M3_DIO BY LAYER
}

A.R.7.VIA3 { @ (cumulative VIA1~VIA3 area / gate area) <= OD area x 210 + 900 (with protection OD)
  NET AREA RATIO VIA3 VIA2 VIA1 M3_DIO GATE_VIA > 0 ACCUMULATE ACC_VIA2
       [
        !!AREA(VIA3) * !!AREA(M3_DIO) *
	   ( AREA(VIA3)/AREA(GATE_VIA)-AREA(M3_DIO)*210-900 ) 
       - (!AREA(VIA3)+!AREA(M3_DIO)) * LargeNumber	    
       ] RDB A.R.7.VIA3.rep VIA3 VIA2 VIA1 M3_DIO GATE_VIA BY LAYER
}

A.R.9.VIA3 { @ (cumulative VIA1~VIA3 area / gate area) <= 50 (in OD2/without protection OD)  
  NET AREA RATIO VIA3 VIA2 VIA1 HV_GATE M3_DIO GATE_VIA > 0 ACCUMULATE ACC_VIA2
      [
        !AREA(M3_DIO) * !!AREA(VIA3) * !!AREA(GATE_VIA) * !!AREA(HV_GATE) * ( AREA(VIA3)/AREA(GATE_VIA) - 50 )
        - (!!AREA(M3_DIO)+!AREA(VIA3)+!AREA(GATE_VIA)+!AREA(HV_GATE)) * LargeNumber
	
      ] RDB A.R.9.VIA3.rep VIA3 VIA2 VIA1 GATE_VIA BY LAYER 
}


ACC_VIA3 = NET AREA RATIO VIA3 GATE_VIA >=0 ACCUMULATE ACC_VIA2



//M4 CHECKS
//=========

CONNECT M4 M3 BY VIA3
CONNECT VIA4 M4

M4_DIO = NET AREA SD >= 0


A.R.6_A.R.8.M4 { @ (cumulative M1~M4 area / gate area) <= (1000 in OD_18 ,5000 not in OD_18) (without protection OD) 
		 @ (cumulative M1~M4 area / gate area) <= OD area x 456 + 43000 (with effective diode) 
  NET AREA RATIO M4 M3 M2 M1 M4_DIO HV18_GATE GATE > 0 ACCUMULATE ACC_M3
       [
       !!AREA(M4) * !!AREA(GATE) *
	   (!AREA(M4_DIO) * (!!AREA(HV18_GATE)*(AREA(M4)/AREA(GATE)-1000) +
	                     !AREA(HV18_GATE)*(AREA(M4)/AREA(GATE)-5000)) +
	    !!AREA(M4_DIO) * ( AREA(M4)/AREA(GATE)-AREA(M4_DIO)*456-43000 ))
       - (!AREA(M4)+!AREA(GATE)) * LargeNumber	    
       ] RDB A.R.6_A.R.8.M4.rep M4 M3 M2 M1 M4_DIO HV18_GATE GATE BY LAYER
}

ACC_M4 = NET AREA RATIO M4 GATE >= 0 ACCUMULATE ACC_M3

//VIA4 CHECKS
//===========

A.R.4.VIA4 { @ (VIA4 area / gate area) <= 20 (without protection OD)
  NET AREA RATIO VIA4 GATE_VIA M4_DIO > 20
      [
        !AREA(M4_DIO) * !!AREA(VIA4) * !!AREA(GATE_VIA) * ( AREA(VIA4)/AREA(GATE_VIA) )
      ] RDB A.R.4.VIA4.rep VIA4 GATE_VIA M4_DIO BY LAYER
}

A.R.7.VIA4 { @ (cumulative VIA1~VIA4 area / gate area) <= OD area x 210 + 900 (with protection OD)
  NET AREA RATIO VIA4 VIA3 VIA2 VIA1 M4_DIO GATE_VIA > 0 ACCUMULATE ACC_VIA3
       [
        !!AREA(VIA4) * !!AREA(M4_DIO) *
	   ( AREA(VIA4)/AREA(GATE_VIA)-AREA(M4_DIO)*210-900 ) 
       - (!AREA(VIA4)+!AREA(M4_DIO)) * LargeNumber	    
       ] RDB A.R.7.VIA4.rep VIA4 VIA3 VIA2 VIA1 M4_DIO GATE_VIA BY LAYER
}

A.R.9.VIA4 { @ (cumulative VIA1~VIA4 area / gate area) <= 50 (in OD2/without protection OD)  
  NET AREA RATIO VIA4 VIA3 VIA2 VIA1 HV_GATE M4_DIO GATE_VIA > 0 ACCUMULATE ACC_VIA3
      [
        !AREA(M4_DIO) * !!AREA(VIA4) * !!AREA(GATE_VIA) * !!AREA(HV_GATE) * ( AREA(VIA4)/AREA(GATE_VIA) - 50 )
        - (!!AREA(M4_DIO)+!AREA(VIA4)+!AREA(GATE_VIA)+!AREA(HV_GATE)) * LargeNumber
	
      ] RDB A.R.9.VIA4.rep VIA4 VIA3 VIA2 VIA1 GATE_VIA BY LAYER 
}


ACC_VIA4 = NET AREA RATIO VIA4 GATE_VIA >=0 ACCUMULATE ACC_VIA3



//M5 CHECKS
//=========

CONNECT M5 M4 BY VIA4
CONNECT VIA5 M5

M5_DIO = NET AREA SD >= 0


A.R.6_A.R.8.M5 { @ (cumulative M1~M5 area / gate area) <= (1000 in OD_18 ,5000 not in OD_18) (without protection OD) 
		 @ (cumulative M1~M5 area / gate area) <= OD area x 456 + 43000 (with effective diode) 
  NET AREA RATIO M5 M4 M3 M2 M1 M5_DIO HV18_GATE GATE > 0 ACCUMULATE ACC_M4
       [
       !!AREA(M5) * !!AREA(GATE) *
	   (!AREA(M5_DIO) * (!!AREA(HV18_GATE)*(AREA(M5)/AREA(GATE)-1000) +
	                     !AREA(HV18_GATE)*(AREA(M5)/AREA(GATE)-5000)) +
	    !!AREA(M5_DIO) * ( AREA(M5)/AREA(GATE)-AREA(M5_DIO)*456-43000 ))
       - (!AREA(M5)+!AREA(GATE)) * LargeNumber	    
       ] RDB A.R.6_A.R.8.M5.rep M5 M4 M3 M2 M1 M5_DIO HV18_GATE GATE BY LAYER
}

ACC_M5 = NET AREA RATIO M5 GATE >= 0 ACCUMULATE ACC_M4

//VIA5 CHECKS
//===========

A.R.4.VIA5 { @ (VIA5 area / gate area) <= 20 (without protection OD)
  NET AREA RATIO VIA5 GATE_VIA M5_DIO > 20
      [
        !AREA(M5_DIO) * !!AREA(VIA5) * !!AREA(GATE_VIA) * ( AREA(VIA5)/AREA(GATE_VIA) )
      ] RDB A.R.4.VIA5.rep VIA5 GATE_VIA M5_DIO BY LAYER
}

A.R.7.VIA5 { @ (cumulative VIA1~VIA5 area / gate area) <= OD area x 210 + 900 (with protection OD)
  NET AREA RATIO VIA5 VIA4 VIA3 VIA2 VIA1 M5_DIO GATE_VIA > 0 ACCUMULATE ACC_VIA4
       [
        !!AREA(VIA5) * !!AREA(M5_DIO) *
	   ( AREA(VIA5)/AREA(GATE_VIA)-AREA(M5_DIO)*210-900 ) 
       - (!AREA(VIA5)+!AREA(M5_DIO)) * LargeNumber	    
       ] RDB A.R.7.VIA5.rep VIA5 VIA4 VIA3 VIA2 VIA1 M5_DIO GATE_VIA BY LAYER
}

A.R.9.VIA5 { @ (cumulative VIA1~VIA5 area / gate area) <= 50 (in OD2/without protection OD)  
  NET AREA RATIO VIA5 VIA4 VIA3 VIA2 VIA1 HV_GATE M5_DIO GATE_VIA > 0 ACCUMULATE ACC_VIA4
      [
        !AREA(M5_DIO) * !!AREA(VIA5) * !!AREA(GATE_VIA) * !!AREA(HV_GATE) * ( AREA(VIA5)/AREA(GATE_VIA) - 50 )
        - (!!AREA(M5_DIO)+!AREA(VIA5)+!AREA(GATE_VIA)+!AREA(HV_GATE)) * LargeNumber
	
      ] RDB A.R.9.VIA5.rep VIA5 VIA4 VIA3 VIA2 VIA1 GATE_VIA BY LAYER 
}


ACC_VIA5 = NET AREA RATIO VIA5 GATE_VIA >=0 ACCUMULATE ACC_VIA4


//M6 CHECKS
//=========

CONNECT M6 M5 BY VIA5
CONNECT RV  M6 
CONNECT CB  M6 
CONNECT CBD M6 

M6_DIO = NET AREA SD >= 0


// M6 is the last metal
A.R.6.M6 { @ (cumulative M1~M6 area / gate area) <= (1000 in OD18,5000 not in OD18) (without protection OD) 
  NET AREA RATIO M6 M5 M4 M3 M2 M1 M6_DIO HV18_GATE GATE > 0 ACCUMULATE ACC_M5
    [
    !!AREA(M6) * !!AREA(GATE) * !AREA(M6_DIO) * 
	(!!AREA(HV18_GATE)*(AREA(M6)/AREA(GATE)-1000) + !AREA(HV18_GATE)*(AREA(M6)/AREA(GATE)-5000))
    - (!AREA(M6)+!AREA(GATE)+!!AREA(M6_DIO)) * LargeNumber	    
    ] RDB A.R.6.M6.rep M6 M5 M4 M3 M2 M1 M6_DIO HV18_GATE GATE BY LAYER
}
A.R.8.M6 { @ (M6 area / gate area) <= OD area x 8000 + 50000 for last metal alone (with protection OD)
  NET AREA RATIO M6 M6_DIO GATE > 0
    [
      !!AREA(M6_DIO) * ( AREA(M6)/AREA(GATE)-AREA(M6_DIO)*8000-50000 )
    ] RDB A.R.8.M6.rep M6 M6_DIO GATE BY LAYER
}

ACC_M6 = NET AREA RATIO M6 GATE >= 0 ACCUMULATE ACC_M5


//RV CHECK 
//=========
A.R.10_A.R.12.RV { @ (RV area / gate area) <= (20 in OD2, 200 not in OD2) (without protection OD)
                   @ (RV area / gate area) <= OD area x 83 + 400 (with protection OD) 
  NET AREA RATIO RV M6_DIO HV_GATE GATE_VIA > 0
       [
         !!AREA(RV) * !!AREA(GATE_VIA) *
           (!AREA(M6_DIO) * (!!AREA(HV_GATE)*(AREA(RV)/AREA(GATE_VIA)-20) +
			     !AREA(HV_GATE)*(AREA(RV)/AREA(GATE_VIA)-200)) +
	    !!AREA(M6_DIO) * ( AREA(RV)/AREA(GATE_VIA)-AREA(M6_DIO)*83-400 ))
       ] RDB A.R.10_A.R.12.RV.rep RV M6_DIO HV_GATE GATE_VIA BY LAYER
}




//AL-RDL CHECKS
//=========

CONNECT AP M6 BY RV
CONNECT AP M6 BY CB
CONNECT AP M6 BY CBD

AP_DIO = NET AREA SD >= 0

#IFDEF 28K_AP
VARIABLE AP_thickness 2.8
#ELSE
VARIABLE AP_thickness 1.45
#ENDIF

A.R.11_A.R.13.AP {@ (AL-RDL sidewall area / gate area) <= (1000 in OD2, 2000 not in OD2) (without protection OD)
                  @ (AL-RDL sidewall area / gate area) <= OD area x 8000 + 30000 (with protection OD)
     NET AREA RATIO AP AP_DIO HV_GATE GATE > 0
       [
       !!AREA(AP) * !!AREA(GATE) *
	   (!AREA(AP_DIO) * (!!AREA(HV_GATE)*(PERIMETER(AP)*AP_thickness/AREA(GATE)-1000) +
			     !AREA(HV_GATE)*(PERIMETER(AP)*AP_thickness/AREA(GATE)-2000)) +
	    !!AREA(AP_DIO) * (PERIMETER(AP)*AP_thickness/AREA(GATE) - AREA(AP_DIO)*8000 - 30000))
       - (!AREA(AP)+!AREA(GATE)) * LargeNumber    
       ] RDB A.R.11_A.R.13.AP.rep AP AP_DIO HV_GATE GATE BY LAYER

}




