//*************************************************************************************
//* CALIBRE MIM ANTENNA DRC COMMAND FILE  - CN65S_7M_5X1Z.MIM_ANT.20a (10/16/2009)
//* FOR TSMC 65NM CMOS 1P7M+APRDL+MIM PROCESS WITH 5X1Z METAL OPTION (MIM BETWEEN M7 AND M6)
//* DESIGN RULE DOCUMENT: 	T-N65-CL-DR-001 	VER 2.0
//* DRC COMMAND FILE DOCUMENT: 	T-N65-CL-DR-001-C1 	VER 2.0a
//*************************************************************************************

//*************************************************************************************
// TSMC has developed this deck using Mentor Graphics proprietary SVRF and TVF formats.
// The deck is to be used only in Calibre tools.
//*************************************************************************************


//#DEFINE 28K_AP


//
// ENVIRONMENT SETUP
//==================
PRECISION    1000
RESOLUTION      5

LAYOUT SYSTEM GDSII
LAYOUT PATH "GDSFILENAME"
LAYOUT PRIMARY "TOPCELLNAME"

DRC RESULTS DATABASE "DRC_RES.db"
DRC SUMMARY REPORT "DRC.rep"
DRC KEEP EMPTY NO
DRC CHECK TEXT ALL
DRC MAXIMUM RESULTS ALL

LAYOUT PROCESS BOX RECORD YES
FLAG OFFGRID YES
FLAG ACUTE YES
FLAG SKEW YES
FLAG NONSIMPLE YES
DRC INCREMENTAL CONNECT YES

VARIABLE LargeNumber 1000000000

#IFDEF 28K_AP
VARIABLE AP_THICKNESS 2.8
#ELSE
VARIABLE AP_THICKNESS 1.45
#ENDIF

// DRAWN LAYER DEFINITIONS
//========================
LAYER  OD_18     16  // define 1.8V thick gate oxides
LAYER  OD_25     18  // define 2.5V thick gate oxides
LAYER  OD_33     15  // define 3.3V thick gate oxides
LAYER  CO        30
LAYER  VIA1      51     // VIA1 Define connect for M2 to M1
LAYER  VIA2      52     // VIA2 Define connect for M3 to M2
LAYER  VIA3      53     // VIA3 Define connect for M4 to M3
LAYER  VIA4      54     // VIA4 Define connect for M5 to M4
LAYER  VIA5      55     // VIA5 Define connect for M6 to M5
LAYER  VIA6      56     // VIA6 Define connect for M7 to M6

LAYER  M1     331  // Metal1 layer
LAYER MAP 31 DATATYPE 0  331  // Mapping (31;0)  to 331 
LAYER MAP 31 DATATYPE 7  331  // Mapping (31;7)  to 331
LAYER MAP 31 DATATYPE 20 331  // Mapping (31;20) to 331 
LAYER MAP 31 DATATYPE 40 331  // Mapping (31;40) to 331
LAYER MAP 31 DATATYPE 60 331  // Mapping (31;60) to 331
LAYER MAP 31 DATATYPE 80 331  // Mapping (31;80) to 331
LAYER  M2     332  // Metal2 layer
LAYER MAP 32 DATATYPE 0  332  // Mapping (32;0)  to 332 
LAYER MAP 32 DATATYPE 7  332  // Mapping (32;7)  to 332
LAYER MAP 32 DATATYPE 20 332  // Mapping (32;20) to 332 
LAYER MAP 32 DATATYPE 40 332  // Mapping (32;40) to 332
LAYER MAP 32 DATATYPE 60 332  // Mapping (32;60) to 332
LAYER MAP 32 DATATYPE 80 332  // Mapping (32;80) to 332
LAYER  M3     333  // Metal3 layer
LAYER MAP 33 DATATYPE 0  333  // Mapping (33;0)  to 333 
LAYER MAP 33 DATATYPE 7  333  // Mapping (33;7)  to 333
LAYER MAP 33 DATATYPE 20 333  // Mapping (33;20) to 333 
LAYER MAP 33 DATATYPE 40 333  // Mapping (33;40) to 333
LAYER MAP 33 DATATYPE 60 333  // Mapping (33;60) to 333
LAYER MAP 33 DATATYPE 80 333  // Mapping (33;80) to 333
LAYER  M4     334  // Metal4 layer
LAYER MAP 34 DATATYPE 0  334  // Mapping (34;0)  to 334 
LAYER MAP 34 DATATYPE 7  334  // Mapping (34;7)  to 334
LAYER MAP 34 DATATYPE 20 334  // Mapping (34;20) to 334 
LAYER MAP 34 DATATYPE 40 334  // Mapping (34;40) to 334
LAYER MAP 34 DATATYPE 60 334  // Mapping (34;60) to 334
LAYER MAP 34 DATATYPE 80 334  // Mapping (34;80) to 334
LAYER  M5     335  // Metal5 layer
LAYER MAP 35 DATATYPE 0  335  // Mapping (35;0)  to 335 
LAYER MAP 35 DATATYPE 7  335  // Mapping (35;7)  to 335
LAYER MAP 35 DATATYPE 20 335  // Mapping (35;20) to 335 
LAYER MAP 35 DATATYPE 40 335  // Mapping (35;40) to 335
LAYER MAP 35 DATATYPE 60 335  // Mapping (35;60) to 335
LAYER MAP 35 DATATYPE 80 335  // Mapping (35;80) to 335
LAYER  M6     336  // Metal6 layer
LAYER MAP 36 DATATYPE 0  336  // Mapping (36;0)  to 336 
LAYER MAP 36 DATATYPE 7  336  // Mapping (36;7)  to 336
LAYER MAP 36 DATATYPE 20 336  // Mapping (36;20) to 336 
LAYER MAP 36 DATATYPE 40 336  // Mapping (36;40) to 336
LAYER MAP 36 DATATYPE 60 336  // Mapping (36;60) to 336
LAYER MAP 36 DATATYPE 80 336  // Mapping (36;80) to 336
LAYER  M7     337  // Metal7 layer
LAYER MAP 37 DATATYPE 0  337  // Mapping (37;0)  to 337 
LAYER MAP 37 DATATYPE 7  337  // Mapping (37;7)  to 337
LAYER MAP 37 DATATYPE 20 337  // Mapping (37;20) to 337 
LAYER MAP 37 DATATYPE 40 337  // Mapping (37;40) to 337
LAYER MAP 37 DATATYPE 60 337  // Mapping (37;60) to 337
LAYER MAP 37 DATATYPE 80 337  // Mapping (37;80) to 337

LAYER AP         74  // AP RDL 
LAYER Cu_PPI     7410 // Cu_PPI interconnection between Polymide1 and Polymide2 for WLCSP
LAYER MAP 74 DATATYPE 10 7410
LAYER RV         85  // RV Define connect for M7 to AP
LAYER CB         76  // CB Define connect for M7 to AP
LAYER CBD        169 // CBD Define connect for M7 to AP
LAYER CBM        88  // Capacitor Bottom metal for MIM capacitor 
LAYER CTM        77  // Capacitor Top metal for MIM capacitor

LAYER MAP 6 DATATYPE 0 350  // Mapping (6;0) to 350 for 
LAYER MAP 7 DATATYPE 0 351  // Mapping (7;0) to 351 for PDIFF
LAYER MAP 8 DATATYPE 0 352  // Mapping (8;0) to 352 for NDIFF
LAYER DIFFi    350
LAYER PDIFFi   351  	    // PDIFF active areas
LAYER NDIFFi   352   	    // NDIFF active areas
LAYER POLY  365
LAYER MAP 17 DATATYPE 0 365  // Mapping (17;0) to 341 
OD = (DIFFi OR PDIFFi) OR NDIFFi

LAYOUT TOP LAYER M1 VIA1 
LAYOUT TOP LAYER M2 VIA2 
LAYOUT TOP LAYER M3 VIA3 
LAYOUT TOP LAYER M4 VIA4 
LAYOUT TOP LAYER M5 VIA5 
LAYOUT TOP LAYER M6 VIA6 
LAYOUT TOP LAYER M7 AP RV CB CBM CTM 

GATE     = OD   AND POLY
SD       = OD   NOT POLY
CO_PO    = CO AND POLY

CONNECT GATE POLY
CONNECT M1 POLY SD BY CO
CONNECT M2 M1 BY VIA1
CONNECT M3 M2 BY VIA2
CONNECT M4 M3 BY VIA3
CONNECT M5 M4 BY VIA4
CONNECT M6 M5 BY VIA5




VCAP_CTM = VIA6 AND CTM 
VCAP_CBM = (VIA6 NOT CTM) AND CBM 
CTMc = COPY CTM 
CONNECT VCAP_CTM CTM
CONNECT VCAP_CBM CBM
CONNECT CTMc CBM

A.R.MIM.4.VIA6.CTM { @ Maximun ratio of the cumulative via area to the MIM capacitor for balanced structure when both CTM and CBM are floating  <= 20 in CTM node
  NET AREA RATIO VCAP_CTM CTM > 0
       [!!AREA(CTM) * (AREA(VCAP_CTM)/AREA(CTM)-20) ] 
       RDB A.R.MIM.4.VIA6.CTM.rep VCAP_CTM CTM BY LAYER
}



A.R.MIM.4.VIA6.CBM { @  Maximun ratio of the cumulative via area to the MIM capacitor for balanced structure when both CTM and CBM are floating  <= 20 in CBM node
    NET AREA RATIO VCAP_CBM CTMc > 0
       [!!AREA(CTMc) * (AREA(VCAP_CBM)/AREA(CTMc)-20) ] 
       RDB A.R.MIM.4.VIA6.CBM.rep VCAP_CBM CTMc BY LAYER
}

ACC_VIA6_CTM = NET AREA RATIO VCAP_CTM CTM > 0 ACCUMULATE
ACC_VIA6_CTMc = NET AREA RATIO VCAP_CBM CTMc > 0 ACCUMULATE



CONNECT M7 CTM CBM M6 BY VIA6

CONNECT RV M7

CTM_M7_W_OD = NET AREA RATIO CTM SD > 0                 //CTM connecting  OD
CBM_M7_W_OD = NET AREA RATIO CBM SD > 0                 //CBM connecting  OD
CTM_M7_W_GATE = NET AREA RATIO CTM GATE > 0             //CTM connecting  GATE
CBM_M7_W_GATE = NET AREA RATIO CBM GATE > 0             //CBM connecting  GATE
CTM_M7_FLOAT = CTM NOT (CTM_M7_W_OD OR CTM_M7_W_GATE) //CTM without connect to OD
CBM_M7_FLOAT = CBM NOT (CBM_M7_W_OD OR CTM_M7_W_GATE) //CBM without connect to OD
CTM_M7_1 = CTM_M7_FLOAT INTERACT CBM_M7_FLOAT 
CTM_M7_2 = CTM_M7_W_OD  INTERACT CBM_M7_W_OD 
Bal_CTM_M7 = CTM_M7_1 OR CTM_M7_2           		//Balanced MIM 
unBal_CTM_M7 = CTM NOT Bal_CTM_M7            		//unBalanced MIM 
CTM_M7_FLOATING = COPY CTM_M7_FLOAT          		// floating CTM, reset connection
CBM_M7_FLOATING = COPY CBM_M7_FLOAT          		// floating CBM, reset connection
CTM_M7_CONNECTED = COPY CTM_M7_W_OD          		// connected CTM , reset connection
CBM_M7_CONNECTED = COPY CBM_M7_W_OD          		// connected CBM , reset connection

MIM_M7_FLOATING_CTM = CTM_M7_FLOATING INTERACT CBM_M7_FLOATING   
MIM_M7_FLOATING_CTMc = COPY MIM_M7_FLOATING_CTM
CONNECT MIM_M7_FLOATING_CTM CTM
CONNECT MIM_M7_FLOATING_CTMc CBM

MIM_M7_CONNECTED_CTM = CTM_M7_CONNECTED INTERACT CBM_M7_CONNECTED   
MIM_M7_CONNECTED_CTMc = COPY MIM_M7_CONNECTED_CTM
CONNECT MIM_M7_CONNECTED_CTM CTM
CONNECT MIM_M7_CONNECTED_CTMc CBM

M7_DIO = NET AREA SD >= 0

A.R.MIM.1.M7 { @ Unbalanced structure is not allowed.  
     A = STAMP unBal_CTM_M7 BY CTM
     NET AREA RATIO A M7 >0 [!!AREA(M7)*AREA(A)]
     B = STAMP unBal_CTM_M7 BY CBM
     NET AREA RATIO B M7 >0 [!!AREA(M7)*AREA(B)]
     NET AREA RATIO CTM M7 GATE SD > 0 [!!AREA(M7)*!!AREA(CTM)*!!AREA(GATE)*!AREA(SD)] 
     NET AREA RATIO CBM M7 GATE SD > 0 [!!AREA(M7)*!!AREA(CBM)*!!AREA(GATE)*!AREA(SD)]
}



A.R.MIM.2.M7.CTM { @ Maximun ratio of the cumulative metal area to the MIM capacitor for balanced structure when both CTM and CBM are floating  <= 1000 in CTM node
  NET AREA RATIO M7 MIM_M7_FLOATING_CTM > 0
       [!!AREA(MIM_M7_FLOATING_CTM) *
	(AREA(M7)/AREA(MIM_M7_FLOATING_CTM)-1000) ] 
       RDB A.R.MIM.2.M7.CTM.rep M7 MIM_M7_FLOATING_CTM BY LAYER
}



A.R.MIM.2.M7.CBM { @ Maximun ratio of the cumulative metal area to the MIM capacitor for balanced structure when both CTM and CBM are floating  <= 1000 in CBM node
  NET AREA RATIO M7 MIM_M7_FLOATING_CTMc > 0
       [!!AREA(MIM_M7_FLOATING_CTMc) *
	(AREA(M7)/AREA(MIM_M7_FLOATING_CTMc)-1000) ] 
       RDB A.R.MIM.2.M7.CBM.rep M7 MIM_M7_FLOATING_CTMc BY LAYER
}


 
A.R.MIM.3.M7.CTM { @ Maximun ratio of the cumulative metal area to the MIM capacitor for balanced structure when both CTM and CBM are connnected.  <= OD area x 8000 + 1000 in CTM node
  NET AREA RATIO M7 MIM_M7_CONNECTED_CTM M7_DIO > 0
       [!!AREA(MIM_M7_CONNECTED_CTM) *
	(AREA(M7)/AREA(MIM_M7_CONNECTED_CTM)-AREA(M7_DIO)*8000-1000) ] 
       RDB A.R.MIM.3.M7.CTM.rep M7 MIM_M7_CONNECTED_CTM M7_DIO BY LAYER
}

A.R.MIM.3.M7.CBM { @ Maximun ratio of the cumulative metal area to the MIM capacitor for balanced structure when both CTM and CBM are connnected.  <= OD area x 8000 + 1000 in CBM node
  NET AREA RATIO M7 MIM_M7_CONNECTED_CTMc M7_DIO > 0
       [!!AREA(MIM_M7_CONNECTED_CTMc) *
	(AREA(M7)/AREA(MIM_M7_CONNECTED_CTMc)-AREA(M7_DIO)*8000-1000) ] 
       RDB A.R.MIM.3.M7.CBM.rep M7 MIM_M7_CONNECTED_CTMc M7_DIO BY LAYER
}

ACC_M7_CTM = NET AREA RATIO M7 CTM > 0 ACCUMULATE

ACC_M7_CTMc = NET AREA RATIO M7 CTMc > 0 ACCUMULATE







A.R.MIM.4.RV.CTM { @ Maximun ratio of the cumulative via and RV area to the MIM capacitor for balanced structure when both CTM and CBM are floating  <= 20 in CTM node
  NET AREA RATIO RV MIM_M7_FLOATING_CTM CTM > 0 ACCUMULATE ACC_VIA6_CTM
       [!!AREA(MIM_M7_FLOATING_CTM) *
	(AREA(RV)/AREA(MIM_M7_FLOATING_CTM)-20) ] 
       RDB A.R.MIM.4.RV.CTM.rep RV MIM_M7_FLOATING_CTM BY LAYER
}



A.R.MIM.4.RV.CBM { @  Maximun ratio of the cumulative via and RV area to the MIM capacitor for balanced structure when both CTM and CBM are floating  <= 20 in CBM node
    NET AREA RATIO RV MIM_M7_FLOATING_CTMc CTMc > 0 ACCUMULATE ACC_VIA6_CTMc
       [!!AREA(MIM_M7_FLOATING_CTMc) *
	(AREA(RV)/AREA(MIM_M7_FLOATING_CTMc)-20) ] 
       RDB A.R.MIM.4.RV.CBM.rep RV MIM_M7_FLOATING_CTMc BY LAYER
}

A.R.MIM.5.RV.CTM { @ Maximun ratio of the cumulative via and RV area to the MIM capacitor for balanced structure when both CTM and CBM are connnected.  <= OD area x 210 + 20 in CTM node
  NET AREA RATIO RV MIM_M7_CONNECTED_CTM M7_DIO CTM > 0 ACCUMULATE ACC_VIA6_CTM
       [!!AREA(MIM_M7_CONNECTED_CTM) *
	(AREA(RV)/AREA(MIM_M7_CONNECTED_CTM)-AREA(M7_DIO)*210-20) ] 
       RDB A.R.MIM.5.RV.CTM.rep RV MIM_M7_CONNECTED_CTM M7_DIO BY LAYER
}

A.R.MIM.5.RV.CBM { @ Maximun ratio of the cumulative via RV area to the MIM capacitor for balanced structure when both CTM and CBM are connnected.  <= OD area x 210 + 20 in CBM node
    NET AREA RATIO RV MIM_M7_CONNECTED_CTMc M7_DIO CTMc > 0 ACCUMULATE ACC_VIA6_CTMc
       [!!AREA(MIM_M7_CONNECTED_CTMc) *
	(AREA(RV)/AREA(MIM_M7_CONNECTED_CTMc)-AREA(M7_DIO)*210-20) ] 
       RDB A.R.MIM.5.RV.CBM.rep RV MIM_M7_CONNECTED_CTMc M7_DIO BY LAYER
}


CONNECT AP M7 BY RV
CONNECT AP M7 BY CB
CONNECT AP M7 BY CBD

CTM_AP_W_OD = NET AREA RATIO CTM SD > 0                 //CTM connecting  OD
CBM_AP_W_OD = NET AREA RATIO CBM SD > 0                 //CBM connecting  OD
CTM_AP_W_GATE = NET AREA RATIO CTM GATE > 0             //CTM connecting  GATE
CBM_AP_W_GATE = NET AREA RATIO CBM GATE > 0             //CBM connecting  GATE
CTM_AP_FLOAT = CTM NOT (CTM_AP_W_OD OR CTM_AP_W_GATE) //CTM without connect to OD
CBM_AP_FLOAT = CBM NOT (CBM_AP_W_OD OR CTM_AP_W_GATE) //CBM without connect to OD
CTM_AP_1 = CTM_AP_FLOAT INTERACT CBM_AP_FLOAT 
CTM_AP_2 = CTM_AP_W_OD  INTERACT CBM_AP_W_OD 
Bal_CTM_AP = CTM_AP_1 OR CTM_AP_2            //Balanced MIM 
unBal_CTM_AP = CTM NOT Bal_CTM_AP            //unBalanced MIM 
CTM_AP_FLOATING = COPY CTM_AP_FLOAT          // floating CTM 
CBM_AP_FLOATING = COPY CBM_AP_FLOAT          // floating CBM 
CTM_AP_CONNECTED = COPY CTM_AP_W_OD          // connected CTM 
CBM_AP_CONNECTED = COPY CBM_AP_W_OD          // connected CBM 

MIM_AP_FLOATING_CTM = CTM_AP_FLOATING INTERACT CBM_AP_FLOATING   
MIM_AP_FLOATING_CTMc = COPY MIM_AP_FLOATING_CTM
CONNECT MIM_AP_FLOATING_CTM CTM
CONNECT MIM_AP_FLOATING_CTMc CBM


MIM_AP_CONNECTED_CTM = CTM_AP_CONNECTED INTERACT CBM_AP_CONNECTED   
MIM_AP_CONNECTED_CTMc = COPY MIM_AP_CONNECTED_CTM
CONNECT MIM_AP_CONNECTED_CTM CTM
CONNECT MIM_AP_CONNECTED_CTMc CBM

AP_DIO = NET AREA SD >= 0


A.R.MIM.1.AP { @ Unbalanced structure is not allowed.  
   A = STAMP unBal_CTM_AP BY CTM
   NET AREA RATIO A AP >0 [!!AREA(AP)*AREA(A)]
   B = STAMP unBal_CTM_AP BY CBM
   NET AREA RATIO B AP >0 [!!AREA(AP)*AREA(B)]
   NET AREA RATIO CTM AP GATE SD > 0 [!!AREA(AP)*!!AREA(CTM)*!!AREA(GATE)*!AREA(SD)] 
   NET AREA RATIO CBM AP GATE SD > 0 [!!AREA(AP)*!!AREA(CBM)*!!AREA(GATE)*!AREA(SD)]
}

A.R.MIM.2.AP.CTM { @ Maximun ratio of the cumulative metal area and AP-MD sidewall area to the MIM capacitor for balanced structure when both CTM and CBM are floating  <= 1000 in CTM node
  NET AREA RATIO AP M7 MIM_AP_FLOATING_CTM CTM > 0 ACCUMULATE ACC_M7_CTM
       [!!AREA(AP)*!!AREA(MIM_AP_FLOATING_CTM) *
	(PERIMETER(AP)*AP_THICKNESS/AREA(MIM_AP_FLOATING_CTM)-1000)
	- (!AREA(AP)+!AREA(MIM_AP_FLOATING_CTM)) * LargeNumber ] 
       RDB A.R.MIM.2.AP.CTM.rep AP M7 MIM_AP_FLOATING_CTM CTM BY LAYER
}



A.R.MIM.2.AP.CBM { @ Maximun ratio of the cumulative metal area and AP-MD sidewall area to the MIM capacitor for balanced structure when both CTM and CBM are floating  <= 1000 in CBM node
  NET AREA RATIO AP M7 MIM_AP_FLOATING_CTMc CTMc > 0 ACCUMULATE ACC_M7_CTMc
       [!!AREA(AP)*!!AREA(MIM_AP_FLOATING_CTMc) *
	(PERIMETER(AP)*AP_THICKNESS/AREA(MIM_AP_FLOATING_CTMc)-1000) 
	- (!AREA(AP)+!AREA(MIM_AP_FLOATING_CTMc)) * LargeNumber ] 
       RDB A.R.MIM.2.AP.CBM.rep AP M7 MIM_AP_FLOATING_CTMc CTMc BY LAYER
}


 
A.R.MIM.3.AP.CTM { @ Maximun ratio of the cumulative metal area and AP-MD sidewall area to the MIM capacitor for balanced structure when both CTM and CBM are connnected.  <= OD area x 8000 + 1000 in CTM node
  NET AREA RATIO AP M7 MIM_AP_CONNECTED_CTM AP_DIO CTM > 0 ACCUMULATE ACC_M7_CTM
       [!!AREA(AP)*!!AREA(MIM_AP_CONNECTED_CTM) *
	(PERIMETER(AP)*AP_THICKNESS/AREA(MIM_AP_CONNECTED_CTM)-AREA(AP_DIO)*8000-1000) 
	- (!AREA(AP)+!AREA(MIM_AP_CONNECTED_CTM)) * LargeNumber ] 
       RDB A.R.MIM.3.AP.CTM.rep AP M7 MIM_AP_CONNECTED_CTM AP_DIO CTM BY LAYER
}

A.R.MIM.3.AP.CBM { @ Maximun ratio of the cumulative metal area and AP-MD sidewall area to the MIM capacitor for balanced structure when both CTM and CBM are connnected.  <= OD area x 8000 + 1000 in CBM node
  NET AREA RATIO AP M7 MIM_AP_CONNECTED_CTMc AP_DIO CTMc > 0 ACCUMULATE ACC_M7_CTMc
       [!!AREA(AP)*!!AREA(MIM_AP_CONNECTED_CTMc) *
	(PERIMETER(AP)*AP_THICKNESS/AREA(MIM_AP_CONNECTED_CTMc)-AREA(AP_DIO)*8000-1000) 
	- (!AREA(AP)+!AREA(MIM_AP_CONNECTED_CTMc)) * LargeNumber ] 
       RDB A.R.MIM.3.AP.CBM.rep AP M7 MIM_AP_CONNECTED_CTMc AP_DIO CTMc BY LAYER
}

